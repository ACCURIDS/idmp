prefix afn:   <http://jena.apache.org/ARQ/function#>
prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
prefix lcc-cr: <https://www.omg.org/spec/LCC/Countries/CountryRepresentation/>

##
# banner Text should not use special characters

SELECT ?error
WHERE {
  ?subject ?p ?o .
  FILTER NOT EXISTS {?o owl:deprecated "true"^^xsd:boolean} .
  FILTER (DATATYPE(?o)=xsd:string)
  FILTER regex(str(?subject), <HYGIENE_TESTS_FILTER_PARAMETER>)
  FILTER (?p != lcc-lr:hasIndigenousName)
  FILTER (?p != lcc-cr:hasRemarks)
  BIND (afn:localname (?p) AS ?prop)
  BIND ("[ÁáÇçÉéÍíÕõÖöäÄèÈμΜσΣʻa-zA-Z\\\\;'?@$%#&:/\"<*>,._+÷=)(\\[\\]{}0-9\n\t -]" AS ?reg)
  BIND (REPLACE (STR(?o), ?reg, "")  AS ?bads)
  FILTER (?bads != "")
  BIND (concat ("PRODERROR:", STR(?o), " has a bad character |", ?bads, "|", " for property ", STR(?p))
	  AS ?error)
 } 
