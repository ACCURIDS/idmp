prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

##
# banner We should provide all required properties for examples.

SELECT DISTINCT ?warning ?instance
WHERE
{
    ?class rdfs:subClassOf ?restriction.
    ?restriction rdf:type owl:Restriction.
    {
        ?restriction owl:onProperty ?property.
        ?restriction owl:onClass ?restrictingClass.
        ?restriction owl:minQualifiedCardinality ?cardinality.
    }
    UNION
    {
        ?restriction owl:onProperty ?property.
        ?restriction owl:someValuesFrom ?restrictingClass.
        BIND (1 as ?cardinality).
    }
    ?instance rdf:type ?class .
    FILTER NOT EXISTS {?instance ?property []}
    FILTER (?cardinality > 0)

    BIND (concat ("WARN: ", STR(?instance), " looks like an unfinished example.") AS ?warning)
}
ORDER BY ?instance